%===================================================%
%
% Transacción	: jsa_enrolar4D.atx
% Autor			: Jorge F. Sanders A.
% Fecha			: 10/09/2018 - 09:52
% Propósito		: Procedimiento de enrolamiento para identidades para 4 dedos
%						: Los dedos son: índice derecho, pulgar derecho, índice izquierdo 
%						: y pulgar izquierdo. El 1ro. de cualquiera de éstos dedos que logre
%						: obtener un patrón para enrolar, termina el proceso. (1-0,6-5)
%
%===================================================%

%-- Parámetros de entrada y salida
input { string inRut, inNombres, inApPaterno, inApMaterno, inSexo, inFecNac, inTipoId, inId, inCedula; };
output { string ouErrDesc, ouAuditoria; integer ouErrCode; };

%-- Variables locales
global [client]
{
	blob lbBmpHuella, lbPatronEnro, lbWsqHuellaEnro, lbWsqHuellaInversaEnro, lbWsqHuellaInversa;

	buffer lbfB5, lbfB6;
	
	string lsMsg, lsNombreCompleto, lsNombreDedo, lsRevisarDedos, lsDedo;
	
	integer lnErrCodeEnrola, lnAncho, lnAlto, lnFlags, lnItera, lnDedo, lnTolerancia, lnPatron, lnK, lnNumIntentos;
	
	resource lrBmp_ack			= "./images/bmp-ack.bmp";
	resource lrBmp_nak			= "./images/bmp-nak.bmp";
	resource lrBmp_quest		= "./images/bmp-quest.bmp";
	resource lrBmp_vacio		= "./images/bmp-vacio.bmp";
	resource lrBmp_top			= "./images/TOP.jpg";
	resource lrBmp_mano_izq	= "./dedos/000 mano izquierda.gif";
	resource lrBmp_mano_der	= "./dedos/000 mano derecha.gif";
	resource lrBmp_dedo_0		= "./dedos/001 pulgar derecho.gif";
	resource lrBmp_dedo_1		= "./dedos/002 indice derecho.gif";
	resource lrBmp_dedo_2		= "./dedos/003 medio derecho.gif";
	resource lrBmp_dedo_3		= "./dedos/004 anular derecho.gif";
	resource lrBmp_dedo_4		= "./dedos/005 menique derecho.gif";
	resource lrBmp_dedo_5		= "./dedos/006 pulgar izquierdo.gif";
	resource lrBmp_dedo_6		= "./dedos/007 indice izquierdo.gif";
	resource lrBmp_dedo_7		= "./dedos/008 medio izquierdo.gif";
	resource lrBmp_dedo_8		= "./dedos/009 anular izquierdo.gif";
	resource lrBmp_dedo_9		= "./dedos/010 menique izquierdo.gif";
	 
};

procedure [server] GrabaAuditoria()
begin
	Audit.Write ();
end;

procedure [server] GrabaLog()
begin
	Log.Write ();
end;

% Auditoria
procedure [client] Audita()
begin
	Audit.Init ();
	Audit.Origen      = "jsa_enrolar4D";
	Audit.Resultado   = string(Sys.erc "%.004d");
	Audit.Descripcion = "Rut: ",User.Rut;
	Audit.Version     = "2.0";
	Audit.AddField ("Rut", User.Rut);
	Audit.AddField ("Nombre", User.Nombre );
	%if (Re="") then Audit.AddField ("Enrolado",fReEnrolado	)
	%else Audit.AddField ("ReEnrolado",fReEnrolado	);
	Audit.AddField ("Dedo.Fecha", User.Dedo.Fecha);
	Audit.AddField ("Dedo.inx", User.Dedo.inx);
	Audit.AddField ("Dedo.id", User.Dedo.id);
	Audit.AddField ("Dedo.Enrolador",User.Dedo.Enrolador.Institucion);
%	Audit.AddField("Dedo.wsq", User.Dedo.wsq); 
	Audit.AddField("Dedo.wsq", lbWsqHuellaEnro); 
	Audit.AddField ("NombreOper",Sesion.NombreOper);
	Audit.AddField ("Firma.cedula",inCedula);
	Sys.DateTime = DateTime(1);
	GrabaAuditoria();
end;

% Log
procedure [client] Log()
begin
	Log.Pais = User.Pais;
	Log.Rut = User.Rut;
	Log.TipoOperacion = "Trx: jsa_enrolar4D";
	Log.Institucion = Sesion.Institucion;	
	Log.Resultado = "jsa_enrolar4D: OK";
	Log.Descripcion = "ENROLADO";
	Log.Descripcion   = Log.Descripcion," | Dedo: ",User.Dedo.id;
	Log.Descripcion   = Log.Descripcion," | AuditKey: ",Audit.Key;
	Log.Descripcion   = Log.Descripcion," | Lugar recibido: IMRD";
	Log.Descripcion   = Log.Descripcion," | Lugar sesion: ", Sesion.Ubicacion;
	GrabaLog ();
end;

% Diálogo para enrolar
Dialog DlgEnrolar
{
	At 10:20;
	Palette{00:   0,   0,   0;
			01: 255, 255, 255;
			03: 255, 204, 102;
			02: 170, 046,  63;
			04: 0x31, 0x4A, 0x83;	%--> azul oscuro
			05: 0x3D, 0x5E, 0xA5;	%--> azul claro
	};

	Font "MS Sans Serif", size 0*8;
	Title "Enrolamiento de huella [SAE]";
	Background 5 (395) 4;
	Size 408*262;	%Tamaño en versión estable de autentia

	Picture picTop , at 0:0;
	Picture picHuella, at 270:57, size 128*128, style border;
	Font "MS Sans Serif", size 0*16, Bold;
	Label labRut="",    at 7:55, size 248*13;
	Label labNombre="", at 7:71, size 249*26;
	Picture $IDB_Raya, at 7:99;
	Font "Arial", size 0*20, Bold;
	Label labMsg="", at 7:112, size 250*30;
	Font "Lucida Console", size 0*11;
	Label labFrase="", at 7:150, size 250*100;

	Picture Tick1 = $IDB_Quest, at 306:246;
	Picture Tick2 = $IDB_Vacio, at 323:246;
	Picture Tick3 = $IDB_Vacio, at 340:246;
	Picture Tick4 = $IDB_Vacio, at 357:246;
	Picture Tick5, at 354:246, Hide;

	Picture Mano0, at 294:188;
	Picture Mano1, at 336:188;
	
	Button Cancelar, at 61:244, size 50*14, Cancel, 
	On Click
	begin
		Sys.erc = 201;
		Sys.ercDetail = 201;
		Sys.ercText = "Operacion cancelada";
		lnErrCodeEnrola = 201;
		EndDialog(1);
	end;

	On Sensor.Touch:
	begin
		picHuella = NULL;
		labMsg = "capturando...";
		
		if        	(lnItera = 0) then 	Tick1 = lrBmp_quest
		else if 	(lnItera = 1) then 	Tick2 = lrBmp_quest
		else if 	(lnItera = 2) then 	Tick3 = lrBmp_quest
		else                             		Tick4 = lrBmp_quest;
	end;

	On Init:
	begin
		picTop = BMP(lrBmp_top);
		
		% Enrolar Mano derecha (mano izquierda en blanco)
		if (lnDedo = 0) then begin Mano0 = lrBmp_mano_izq; Mano1 = lrBmp_dedo_0; end;
		if (lnDedo = 1) then begin Mano0 = lrBmp_mano_izq; Mano1 = lrBmp_dedo_1; end;
		if (lnDedo = 2) then begin Mano0 = lrBmp_mano_izq; Mano1 = lrBmp_dedo_2; end;
		if (lnDedo = 3) then begin Mano0 = lrBmp_mano_izq; Mano1 = lrBmp_dedo_3; end;
		if (lnDedo = 4) then begin Mano0 = lrBmp_mano_izq; Mano1 = lrBmp_dedo_4; end;
		
		% Enrolar Mano izquierda (mano derecha en blanco)
		if (lnDedo = 5) then begin Mano1 = lrBmp_mano_der; Mano0 = lrBmp_dedo_5; end;
		if (lnDedo = 6) then begin Mano1 = lrBmp_mano_der; Mano0 = lrBmp_dedo_6; end;
		if (lnDedo = 7) then begin Mano1 = lrBmp_mano_der; Mano0 = lrBmp_dedo_7; end;
		if (lnDedo = 8) then begin Mano1 = lrBmp_mano_der; Mano0 = lrBmp_dedo_8; end;
		if (lnDedo = 9) then begin Mano1 = lrBmp_mano_der; Mano0 = lrBmp_dedo_9; end;

		lnAncho = 500;
		lnAlto  = 550;
		lnFlags = 0;
		lnErrCodeEnrola = 0;

		% Labels con el RUT y Nombre
		labRut = inTipoId,": ",inId;
		labNombre = "Sr", if (User.Sexo = "Femenino") then "(it)a" else ""," : ", lsNombreCompleto;

		% Tolerancia para el sensor
		if ((lnTolerancia <> 21) and (lnTolerancia <> 0))  then
			Sensor.Tolerancia = lnTolerancia; 

		Sensor.Open();
		Sensor.Subscribe ();
		Sensor.LoadPatron ();
		
		% Cargar dedo a revisar
		if        	(lnDedo = 0) then lsNombreDedo = "PULGAR DERECHO"
		else if 	(lnDedo = 1) then lsNombreDedo = "INDICE DERECHO"
		else if 	(lnDedo = 2) then lsNombreDedo = "MEDIO DERECHO"
		else if 	(lnDedo = 3) then lsNombreDedo = "ANULAR DERECHO"
		else if 	(lnDedo = 4) then lsNombreDedo = "MEÑIQUE DERECHO"
		else if 	(lnDedo = 5) then lsNombreDedo = "PULGAR IZQUIERDO"
		else if 	(lnDedo = 6) then lsNombreDedo = "INDICE IZQUIERDO"
		else if 	(lnDedo = 7) then lsNombreDedo = "MEDIO IZQUIERDO"
		else if 	(lnDedo = 8) then lsNombreDedo = "ANULAR IZQUIERDO"
		else if 	(lnDedo = 9) then lsNombreDedo = "MEÑIQUE IZQUIERDO";
		
		labMsg = "Por favor, coloque CUATRO VECES el dedo ", lsNombreDedo, " sobre el lector", if (lnTolerancia = 21) then "." else " ";

		lnItera = 0;
		lnPatron = 0;
	end;

	On Sensor.Sample:
	begin
		Beep(0);

		Sensor.GetBmp ( lbBmpHuella, 200, 200 );
%		Sensor.GetBmp ( lbBmpHuella, lnAlto, lnAncho );
		picHuella = BMP (lbBmpHuella);

%		Sensor.GetWsq (lbWsqHuellaEnro,320,460,1);
		Sensor.GetWsq (lbWsqHuellaEnro,lnAlto,lnAncho,1);
%		Sensor.GetWsq (lbWsqHuellaInversaEnro,200,200,1);
		Sensor.GetWsq (lbWsqHuellaInversaEnro,lnAlto,lnAncho,1);
		User.Dedo.Huella = picture(lbWsqHuellaInversa);
		
		Sys.ercText = "";
		Sys.erc = 0;
		
		Sensor.GetVerTemplate ();
		%--> Beep:	0    default
		%-->		0x10 ICONHAND
		%-->		0x20 ICONQUESTION
		%-->		0x30 ICONEXCLAMATION
		%-->		0x40 ICONASTERISK
		
		lnK = 0x30;
		
		if (Sys.erc = 0) then
		begin
			% GetPatron
			Sensor.GetPreTemplate ();
			lnK = 0x40;
		end;

		
		if (Sys.Erc <> 0) Then
		Begin
			labMsg = "Error al validar huella :: ", string(Sys.Erc);
			lnK = 0x30;
			Beep (lnK);
			if      	(lnItera = 0) then 	Tick1 = lrBmp_nak
			else if 	(lnItera = 1) then 	Tick2 = lrBmp_nak
			else if 	(lnItera = 2) then 	Tick3 = lrBmp_nak
			else if 	(lnItera = 3) then 	Tick4 = lrBmp_nak
			else                       				Tick5 = lrBmp_nak;
			return; 
		End;	

		lnItera++;
		
		% Variable bDedo es externa e indica el dedo que se escanea lsNombreDedo
		labMsg = "Muestra ", string(lnItera), " OK, dedo ", lsNombreDedo;
		if (lnItera <= 4) then
		Begin

			% Cargar dedo
			lsRevisarDedos = string(lnDedo);
			Sys.WSConsulta();
			Sensor.LoadPatron ();

			% Revisa si el dedo ingresado ya existe	
			lsDedo = string(lnDedo); 
			Sensor.Verify (lsDedo);

			% Si el dedo se acepta, significa que ya está enrolado
			if ( Sys.erc = 0 ) then begin				
				% Si es un dedo distinto al que se está queriendo enrolar, se rechaza el dedo
				% En sDedo viene como string el número del dedo que hizo match
				User.Dedo = lnDedo;
				%MsgBox "Hizo match ", string(User.Dedo.inx), " ", User.Dedo.id;

				if (User.Dedo.inx <> lnDedo) then
				begin
					MsgBox "El dedo ingresado ya fue enrolado "; %, string(User.Dedo.inx), " ", User.Dedo.id;
					Sys.erc = 1025;
					Sys.ercDetail = 1025;
					Sys.ercText = "Dedo ya enrolado";
					lnErrCodeEnrola = 1025;
					EndDialog (1);
				end;
			end;
		end;
	
		if        	(lnItera = 1) then begin 	Tick1 = lrBmp_ack; Tick2 = lrBmp_quest; end
		else if	(lnItera = 2) then begin 	Tick2 = lrBmp_ack; Tick3 = lrBmp_quest; end
		else if 	(lnItera = 3) then begin 	Tick3 = lrBmp_ack; Tick4 = lrBmp_quest; end
		else                                      		Tick4 = lrBmp_ack;
		
		if (lnItera < 4) then
		begin
			Sensor.Subscribe ();
			return;
		end;

		% Esta ultima parte ocurre sólo cuando i=4, o sea cuando
		% se tienen 4 muestras, suficientes para obtener un patrón
		labMsg = labMsg, LF, "Obteniendo patron #", string(lnPatron + 1), "...";

		Sensor.GetPatron (null, lbPatronEnro);
		
		if (Sys.erc <> 0) then
		begin
			Beep (0x10);
			% Indicar que hubo un error a través de la variable lnErrCodeEnrola
			lnErrCodeEnrola = 1;
			EndDialog(1);
			return;
		end;

		Sensor.GetWsq (lbWsqHuellaEnro, lnAncho, lnAlto, 1 - lnFlags);
		Sensor.GetWsq (lbWsqHuellaInversaEnro, lnAncho, lnAlto, lnFlags);
		User.Dedo.Huella = picture(lbWsqHuellaInversaEnro);
		
		EndDialog (0);
	end;
};

procedure main()
begin

	% Asignar datos de entrada al objeto: "User"
	User.Rut	= inRut;
	User.Pais = "CHILE";
	User.ApPaterno = Upcase(inApPaterno);
	User.ApMaterno = Upcase(inApMaterno);
	User.Nombres = Upcase(inNombres);
	User.FechaNac = inFecNac;
	
	if (inSexo = "M" or inSexo = "Masculino") then User.Sexo = "Masculino" else User.Sexo = "Femenino";

	% Nombre completo
	lsNombreCompleto = User.Nombres, " ", User.ApPaterno, " ", User.ApMaterno;
	User.Nombre = lsNombreCompleto;
	
	% Dedos: 0-pulgar derecho, 1-Indice derecho, 5-pulgar izquierdo, 6-Indice izquierdo. Secuencia dedos: 1-0-6-5
	
	% Este es el primer intento de enrolar el dedo "1"
	lnDedo = 1;
	ouErrCode = Dialog(DlgEnrolar);	
	Sensor.Close ();

	if (lnErrCodeEnrola = 1) or (lnErrCodeEnrola = 201) then
	begin
		% No se logró enrolar el dedo "1", entonces se intenta con el dedo "0"
		msgbox "El proceso de Enrolamiento no logró establecer el patrón dactilar.",CR,LF,"Se intentará enrolar el  PULGAR DERECHO...";
		lnDedo = 0;
		ouErrCode = Dialog(DlgEnrolar);	
		Sensor.Close ();
	end;

	if (lnErrCodeEnrola = 1) or (lnErrCodeEnrola = 201) then
	begin
		% No se logró enrolar el dedo "0", entonces se intenta con el dedo "6"
		msgbox "El proceso de Enrolamiento no logró establecer el patrón dactilar.",CR,LF,"Se intentará enrolar el  INDICE IZQUIERDO...";
		lnDedo = 6;
		ouErrCode = Dialog(DlgEnrolar);	
		Sensor.Close ();
	end;

	if (lnErrCodeEnrola = 1) or (lnErrCodeEnrola = 201) then
	begin
		% No se logró enrolar el dedo "6", entonces se intenta con el dedo "5"
		msgbox "El proceso de Enrolamiento no logró establecer el patrón dactilar.",CR,LF,"Se intentará enrolar el  PULGAR IZQUIERDO...";
		lnDedo = 5;
		ouErrCode = Dialog(DlgEnrolar);	
		Sensor.Close ();
	end;
		
	if (lnErrCodeEnrola = 201) then begin
		ouErrCode = 201;
		ouErrDesc = Sys.ercText;
		ouAuditoria = "S/A";
		return;
	end;
	
	if (lnErrCodeEnrola = 1) then begin
		msgbox "El proceso de Enrolamiento encontró errores en captura de huellas.";
		ouErrCode = 1;
		ouErrDesc = Sys.erctext;
		ouAuditoria = "S/A";
		return;
	end;
 
	if (Sys.erc = 0) then 
	begin
	
		User.Registrado = DateTime(2);
		User.Dedo = lnDedo;
		User.nDedos = 1;
		User.Dedo.inx = lnDedo;
		User.Dedo.Patron = lbPatronEnro;
		%User.Clase = "OPER";
		User.Institucion = Sesion.institucion;
		User.Dedo.Enrolador.RutOper = Sesion.RutOper;
		User.Dedo.wsq = WSQ (User.Dedo.Huella);
		
		% Enrolar
		lsMsg = "";
		Sys.WSEnrola(); % Guarda los datos en TPersonas y TDedos
		
		lsMsg = "Se ejecuto el proceso Sys.WSEnrola ()";
		% Auditoría
		Audita();
		lsMsg = lsMsg, CR, LF, " - Se ejecuto el proceso Audita()";
		ouAuditoria = Audit.Key;
		
		% Log
		Log ();
		lsMsg = lsMsg, CR, LF, " - Se ejecuto el proceso Log()";
		lsMsg = lsMsg, CR, LF, " - Nro de Audotoria = ", ouAuditoria;
		ouErrCode = 0;
		ouErrDesc = lsMsg;
	end;
	
end;